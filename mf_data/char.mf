% a simple E character
Phi=(1+sqrt5)/2;
w=100;
h=Phi*w;
x1=x2=x3=0;
x4=Phi*x5=x6=w;
y1=y6=0;
y3=y4=h;
y2=y5;
(y3-y2)=Phi*(y2-y1);
draw z1..z6;
draw z2..z5;
draw z3..z4;
draw z1..z3;
labels(range 1 thru 6);
showit;
shipit;

% a simple A character
clearit; clearxy;
z1=z5-(w,0)=(0,0);
z3=(.5[x1,x5],h);
y2=y4;
z2=whatever[z1,z3];
z4=whatever[z3,z5];
(y3-y2)=Phi*(y2-y1);
draw z1..z3;
draw z3..z5;
draw z2..z4;
labels(range 1 thru 5);
showit;
shipit;

% a simple A character, without labels
clearit; clearxy;
z1=z5-(w,0)=(0,0);
z3=(.5[x1,x5],h);
y2=y4;
z2=whatever[z1,z3];
z4=whatever[z3,z5];
(y3-y2)=Phi*(y2-y1);
draw z1..z3;
draw z3..z5;
draw z2..z4;
showit;
shipit;

% a simple E character with a Phi-based pen
clearit; clearxy;
x1=x2=x3=0;
x4=Phi*x5=x6=w;
y1=y6=0;
y3=y4=h;
y2=y5;
(y3-y2)=Phi*(y2-y1);
%pickup pencircle scaled 15 yscaled (1/Phi) rotated angle (Phi,1);
pickup pencircle scaled 15 xscaled Phi rotated angle (Phi,1);
draw z1..z6;
draw z2..z5;
draw z3..z4;
draw z1..z3;
labels(range 1 thru 6);
showit;
shipit;

% a simple A character with a Phi-based pen
clearit; clearxy;
z1=z5-(w,0)=(0,0);
z3=(.5[x1,x5],h);
y2=y4;
z2=whatever[z1,z3];
z4=whatever[z3,z5];
(y3-y2)=Phi*(y2-y1);
%pickup pencircle scaled 15 yscaled (1/Phi) rotated angle (Phi,1);
pickup pencircle scaled 15 xscaled Phi rotated angle (Phi,1);
draw z1..z3;
draw z3..z5;
draw z2..z4;
labels(range 1 thru 5);
showit;
shipit;

% a simple incorrect D character with a Phi-based pen
clearit; clearxy;
z1=z3-h*up=origin;
x2=w;
(y3-y2)=Phi*(y2-y1);
pickup pencircle scaled 15 xscaled Phi rotated angle (Phi,1);
draw z1..z3;
draw z1..z2..z3;
labels(range 1 thru 3);
showit;
shipit;

% another simple incorrect D character with a Phi-based pen
clearit; clearxy;
z1=z3-h*up=origin;
x2=w;
(y3-y2)=Phi*(y2-y1);
pickup pencircle scaled 15 xscaled Phi rotated angle (Phi,1);
draw z1..z3;
draw z1{right}..z2..{left}z3;
labels(range 1 thru 3);
showit;
shipit;

% a simple correct D character with a Phi-based pen
clearit; clearxy;
z1=z3-h*up=origin;
x2=w;
(y3-y2)=Phi*(y2-y1);
pickup pencircle scaled 15 xscaled Phi rotated angle (Phi,1);
draw z1..z3;
draw z1{right}..z2{up}..{left}z3;
labels(range 1 thru 3);
showit;
shipit;

% a simple C character with a Phi-based pen
clearit; clearxy;
x3=0;
y4-y3=Phi*(y3-y2);
z1=(w,y3/2Phi);
z2=(w/Phi,0);
z4=z2 reflectedabout ((0,h/2),(w,h/2));
z5=z1 reflectedabout ((0,h/2),(w,h/2));
pickup pencircle scaled 15 xscaled Phi rotated angle (Phi,1);
draw z1..z2{left}..z3{up}..z4{right}..z5;
labels(range 1 thru 5);
showit;
shipit;

% a simple C character with a Phi-based pen, without labels
clearit; clearxy;
x3=0;
y4-y3=Phi*(y3-y2);
z1=(w,y3/2Phi);
z2=(w/Phi,0);
z4=z2 reflectedabout ((0,h/2),(w,h/2));
z5=z1 reflectedabout ((0,h/2),(w,h/2));
pickup pencircle scaled 15 xscaled Phi rotated angle (Phi,1);
draw z1..z2{left}..z3{up}..z4{right}..z5;
showit;
shipit;

end